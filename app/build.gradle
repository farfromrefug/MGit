apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'

android {
    namespace "com.manichord.mgit"
    compileSdk 34
    buildToolsVersion '34.0.0'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        jvmToolchain(17)
    }

    defaultConfig {
        applicationId "com.manichord.mgit"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 241
        versionName "2.0.0"
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        if (project.hasProperty('special')) {
            release {
                keyAlias alias
                keyPassword password
                storeFile file(keystore)
                storePassword password
            }
        } else {
            release {
                keyAlias ''
                keyPassword ''
                storeFile file('/empty')
                storePassword ''
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == "com.jcraft" && details.requested.name == "jsch") {
            details.useTarget("com.github.mwiede:jsch:0.2.0")
        }

    }
    exclude module: 'httpclient'
}


dependencies {

    def acraVersion = '5.11.3'

    implementation 'androidx.fragment:fragment:1.7.1'
    implementation 'androidx.annotation:annotation:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.vectordrawable:vectordrawable:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.8.0'

    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation 'com.github.mwiede:jsch:0.2.17'
    implementation 'commons-io:commons-io:2.13.0'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:3.7.1.201504261725-r'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'com.scottyab:secure-preferences-lib:0.1.7'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'

    implementation "ch.acra:acra-mail:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"

    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-timber:1.5.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.5'
    testImplementation 'org.robolectric:shadows-support-v4:3.4-rc2'
}
